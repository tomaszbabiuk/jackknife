group = 'com.github.tomaszbabiuk'
version = '0.1.0'

Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
properties.load(inputStream)

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
}

repositories {
    google()
    jcenter()
}


def siteUrl = 'https://github.com/tomaszbabiuk/pomegranate'
def gitUrl = 'https://github.com/tomaszbabiuk/pomegranate.git'

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:support-annotations:28.0.0'
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

def javaApiUrl = 'http://docs.oracle.com/javase/1.7.0/docs/api/'
def groovyApiUrl = 'http://groovy.codehaus.org/gapi/'

tasks.withType(Javadoc) {
    options.links(javaApiUrl, groovyApiUrl)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

//jar.dependsOn sourcesJar

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "tomaszbabiuk"
            name "Tomasz Babiuk"
            email "tomasz.babiuk@gmail.com"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'POMegranate is page object injection framework for android automation tests.')
                root.children().last() + pomConfig
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }
    publish {
        repository {
            repoKey = 'pomegranate'
            username = properties.getProperty('bintray.user')
            password = properties.getProperty('bintray.key')
        }
    }
}

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') {
    doLast {
        logger.info "Installing $project.name"
    }
}

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.key')
    publications = ['mavenJava']
    pkg {
        repo = 'pomegranate'
        name = 'pomegranate-core'
        licenses = ['Apache-2.0']
        desc = 'POMegranate is page object injection framework for android automation tests.'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/tomaszbabiuk/pomegranate/issues'
        vcsUrl = gitUrl
    }
}